#!/usr/bin/perl
use strict;
use warnings;
use Mail::IMAPClient;
use Getopt::Long;

my $usage = "Usage: $0 server[:port] username password [action]\n";
my %opt;
GetOptions( \%opt, 'Debug|debug', 'Ssl|ssl', 'create=s', 'delete=s' )
    or die $usage;

die $usage if @ARGV != 3;

die "Choose between --create or --delete\n"
    if $opt{create} && $opt{delete};

my ( $server, $user, $pass, $folder ) = @ARGV;
my $ssl = $server =~ s/\+$//;
( $server, my $port ) = split /:/, $server;

my $imap = Mail::IMAPClient->new(
    Server => $server,
    $port ? ( Port => $port ) : (),
    User     => $user,
    Password => $pass,
    %opt,
) or die "$@\n";

if ( $opt{create} ) {
    my $folder = $opt{create};
    $imap->create($folder)
        or die "Create '$folder' error: ", $imap->LastError, "\n";
}
elsif ( $opt{delete} ) {
    my $folder = $opt{delete};
    $imap->delete($folder)
        or die "Delete '$folder' error: ", $imap->LastError, "\n";
}
else {
    my $f = $imap->folders;
    print "$_\n" for sort @$f;
}
